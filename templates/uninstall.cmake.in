# CMake build tools for custom algorithms in Varian Eclipse.
#
# Author:    Christopher M Poole <mail@christopherpoole.net>
# Institute: Dept. Ob. Gyn., University of Melbourne, Australia
# Date:      25 August 2014
#
# --- Custom Algorithm Post-uninstallation Configuration ---
#
# This is an automation of the information provided in the Eclipse
# Algorithm API Reference Guide (B503485R01C, December 2011).
#
#   Chapter 4 -> Uninstalling a Custom Algorithm (page 45-46)

# Build a local cache of import variables
set(CMAKE_INSTALL_PREFIX "@CMAKE_INSTALL_PREFIX@")
set(CMAKE_CURRENT_BINARY_DIR "@CMAKE_CURRENT_BINARY_DIR@")

set(SERVANT "@SERVANT@")
set(SERVANT_EXE_NAME "@SERVANT_EXE_NAME@")
set(VERSION "@VERSION@")

set(DCF_DIRECTORY "@DCF_DIRECTORY@")
set(EAAPI_BINARIES "@EAAPI_BINARIES@")
set(OPTIONS_DIRECTORY "@OPTIONS_DIRECTORY@")

set(REGISTER_SERVANT @REGISTER_SERVANT@)
set(REGISTER_ALGORITHM @REGISTER_ALGORITHM@)
set(COPY_CALCULATION_OPTIONS @COPY_CALCULATION_OPTIONS@)
set(FORCE_AGENT_RELOAD @FORCE_AGENT_RELOAD@)
set(COPY_MATERIAL_TABLE @COPY_MATERIAL_TABLE@)

# Delete the physical material table files.
if (${COPY_MATERIAL_TABLE} MATCHES "ON")
    foreach(MATERIAL_TABLE "@INSTALLED_MATERIAL_TABLES@")
        file(REMOVE ${MATERIAL_TABLE})
    endforeach(MATERIAL_TABLE)
endif()

# Delete the calculation options.
if (${COPY_CALCULATION_OPTIONS} MATCHES "ON")
    set(OPTIONS_FILE "${OPTIONS_DIRECTORY}/Options_[API]_${SERVANT}_v${VERSION}.xml")
    file(REMOVE ${OPTIONS_FILE})

    set(OPTIONS_SCHEMA "${OPTIONS_DIRECTORY}/Options_[API]_${SERVANT}_v${VERSION}.xsd")
    file(REMOVE ${OPTIONS_SCHEMA})
endif()

# De-register the servant.
if (${REGISTER_SERVANT} MATCHES "ON")
    string(REPLACE "/" "\\" CONFIG_PATH "VCConfigurationPath=${DCF_DIRECTORY}/server/VCConfiguration.xml")
    string(REPLACE "/" "\\" SERVANT_PATH "ServantXmlPath=${CMAKE_INSTALL_PREFIX}/ServantInfo.xml")

    execute_process(COMMAND "${DCF_DIRECTORY}/server/bin/VCConfig.exe" "deregister-servant" ${CONFIG_PATH} ${SERVANT_PATH}
                    WORKING_DIRECTORY "${DCF_DIRECTORY}")
endif()

# De-register the algorithm.
if (${REGISTER_ALGORITHM} MATCHES "ON")
    string(REPLACE "/" "\\" ALG_INFO "${CMAKE_INSTALL_PREFIX}/AlgorithmInfo.xml")
    string(REPLACE "/" "\\" INSTALLED_ALGS "${DCF_DIRECTORY}/client/InstalledAlgorithms.xml")

    execute_process(COMMAND "${DCF_DIRECTORY}/server/bin/InstallAlgorithm.exe" "uninstall" ${ALG_INFO} ${INSTALLED_ALGS}
                    WORKING_DIRECTORY "${DCF_DIRECTORY}")
endif()

# Uninstall the EAAPI user algorithm.
file(REMOVE "${CMAKE_INSTALL_PREFIX}/${SERVANT_EXE_NAME}.exe")

foreach (BIN "@INSTALLED_EAAPI_BINARIES@")
    file(REMOVE ${BIN})
endforeach(BIN)

file(REMOVE "${CMAKE_INSTALL_PREFIX}/AlgorithmInfo.xml")
file(REMOVE "${CMAKE_INSTALL_PREFIX}/ServantInfo.xml")

# Force DCF agents to reload the configuration.
if (${FORCE_AGENT_RELOAD} MATCHES "ON")
    execute_process(COMMAND "curl -X POST http://localhost:57580")
endif()

# Final clean-up.
file(REMOVE "${CMAKE_INSTALL_PREFIX}/uninstall.cmake")

